# Given a use case find the validation mechanisms,
# implementation and valuable information elements
#
# The mechanism and specification guids link a
# validation to the Java class and method that
# implementing a specific standardized test.
#
# The valuable information elements are linked to
# "fieldsActedUpon" and "fieldsConsulted" via
# annotated method parameters

PREFIX bdqffdq: <https://rs.tdwg.org/bdqffdq/terms/>
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prov: <http://www.w3.org/ns/prov#>

SELECT ?useCase ?mechanism ?specification ?term

WHERE {

    # Find criterion in context from the validation
    # policy for a given use case

    ?policy a bdqffdq:ValidationPolicy .
    ?policy bdqffdq:coversUseCase ?uc .
    ?policy bdqffdq:criterionInContext ?cc .
    ?uc rdfs:label ?useCase .

    # Find the valuable information element
    # terms from the criterion in context

    ?cc bdqffdq:hasInformationElement ?vie .
    ?vie bdqffdq:composedOf ?term .

    # Find the specification from the validation method
    # referencing the criterion in context

    ?vm a bdqffdq:ValidationMethod .
    ?vm bdqffdq:hasContextualizedCriterion ?cc .
    ?vm bdqffdq:hasSpecification ?specification .

    # Find the mechanism from the implementation of
    # the specification

    ?implementation a bdqffdq:Implementation .
    ?implementation bdqffdq:hasSpecification ?specification .
    ?implementation bdqffdq:implementedBy ?mechanism .

    # Filter by a specific use case

    FILTER( ?uc = <urn:uuid:dd78b90c-640f-4b9c-bece-564e525a43e0> )

}
