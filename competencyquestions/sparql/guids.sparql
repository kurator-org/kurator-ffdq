# Given a use case find the validation mechanisms,
# implementation and valuable information elements
#
# The mechanism and specification guids link a
# validation to the Java class and method that
# implementing a specific standardized test.
#
# The valuable information elements are linked to
# "fieldsActedUpon" and "fieldsConsulted" via
# annotated method parameters

PREFIX ffdq: <http://example.com/ffdq/>
PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prov: <http://www.w3.org/ns/prov#>

SELECT ?useCase ?mechanism ?specification ?term

WHERE {

    # Find criterion in context from the validation
    # policy for a given use case

    ?policy a ffdq:ValidationPolicy .
    ?policy ffdq:coversUseCase ?uc .
    ?policy ffdq:criterionInContext ?cc .
    ?uc rdfs:label ?useCase .

    # Find the valuable information element
    # terms from the criterion in context

    ?cc ffdq:hasInformationElement ?vie .
    ?vie ffdq:composedOf ?term .

    # Find the specification from the validation method
    # referencing the criterion in context

    ?vm a ffdq:ValidationMethod .
    ?vm ffdq:hasContextualizedCriterion ?cc .
    ?vm ffdq:hasSpecification ?specification .

    # Find the mechanism from the implementation of
    # the specification

    ?implementation a ffdq:Implementation .
    ?implementation ffdq:hasSpecification ?specification .
    ?implementation ffdq:implementedBy ?mechanism .

    # Filter by a specific use case

    FILTER( ?uc = <urn:uuid:dd78b90c-640f-4b9c-bece-564e525a43e0> )

}